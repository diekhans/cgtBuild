import os
import sys
sys.path.append(os.path.abspath("btools/lib"))

env = Environment()

centos6DevBin = "/opt/rh/devtoolset-3/root/usr/bin"

c99Flags = ["-std=c99"]
cxx11Flags = ["-std=c++11"]
warnOps = ["-Wall", "--pedantic", "-Wno-error=unused-result"]
if os.path.exists(centos6DevBin):
    # hgwdev, use -Werror here
    env.Replace(CC=os.path.join(centos6DevBin, "gcc"))
    env.Append(CFLAGS=c99Flags + warnOps + ["-Werror"])
    env.Replace(CXX=os.path.join(centos6DevBin, "g++"))
    env.Append(CXXFLAGS=cxx11Flags + warnOps + ["-Werror"])
elif os.uname()[0] == "Darwin":
    # Mac
    env.Replace(CC="clang")
    env.Append(CFLAGS=c99Flags + warnOps)
    env.Replace(CXX="clang++")
    env.Append(CXXFLAGS=cxx11Flags + warnOps)
else:
    # modern linux
    env.Replace(CC= "gcc")
    env.Append(CFLAGS=c99Flags + warnOps)
    env.Replace(CXX="g++")
    env.Append(CXXFLAGS=cxx11Flags + warnOps)

variantDir = "build"
env.Replace(LIBPREFIX="lib/")

SConscript("mods/sonLib/sconscript", exports=("env"),
           variant_dir=variantDir, duplicate=False)


